/-
Copyright (c) 2024 TheLeanTeam. All rightPs reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: The Lean Team
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint

/-!
 # Week 7 - diamonds

 In this file, we will learn how to identify and resolve diamonds (type mismatch errors).

 To find the exercises in this file, search (`ctrl/cmd + F`) for `:TODO:`.
-/

section example_intro

/- `NormedAddCommGroup`, `InnerProductSpace`, `Ring`, `Algebra` and `Finite_dimensional`
  gives a mismatch error when trying to deal with linear maps. -/

example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [hE‚ÇÉ : Ring E] [hE‚ÇÑ : Algebra ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) [hE‚ÇÖ : FiniteDimensional ‚ÑÇ E]
  (h : (LinearMap.adjoint T) = T) :
  T = T :=
sorry

/-!
 ## Attempting to fix the error: the naive approach
-/

-- can we be more specific to fix the error?

example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [hE‚ÇÉ : Ring E] [hE‚ÇÑ : Algebra ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) [hE‚ÇÖ : FiniteDimensional ‚ÑÇ E]
  (h : @LinearMap.adjoint ‚ÑÇ E E Complex.instIsROrCComplex hE‚ÇÅ hE‚ÇÅ hE‚ÇÇ hE‚ÇÇ hE‚ÇÖ hE‚ÇÖ T = 0) :
  T = 0 :=
sorry

/- nope
  ... so how do we fix this?!

  According to the Infoview,
  it seems that `AddCommGroup.toAddCommMonoid` is clashing with
  `NonUnitalNonAssocSemiring.toAddCommMonoid`.

  This is because the following are not definitionally equal: -/

example {E : Type*} [hE : NormedAddCommGroup E] [hE‚ÇÇ : Ring E] :
  hE.toAddCommMonoid = hE‚ÇÇ.toAddCommMonoid :=
rfl -- gives an error

-- can we specify which instances the linear map should access?

example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [hE‚ÇÉ : Ring E] [hE‚ÇÑ : Algebra ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) [hE‚ÇÖ : FiniteDimensional ‚ÑÇ E] :
  LinearMap.adjoint (T : @LinearMap ‚ÑÇ ‚ÑÇ _ _ _ E E
    (hE‚ÇÅ.toAddCommMonoid) (hE‚ÇÅ.toAddCommMonoid)
    (NormedSpace.toModule) (NormedSpace.toModule)) = T := sorry

-- nope...
-- maybe we can we fix this by reordering the instances??
example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) [Ring E] [Algebra ‚ÑÇ E] [hE : FiniteDimensional ‚ÑÇ E] :
  LinearMap.adjoint T = T := sorry
-- yes!

-- BUT, what if we needed to define a linear map within the proof?
example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) [Ring E] [Algebra ‚ÑÇ E] [hE : FiniteDimensional ‚ÑÇ E]
  (h : LinearMap.adjoint T = T) : T = 0 := by
{
  let f : E ‚Üí‚Çó[‚ÑÇ] E := sorry
  have : @LinearMap.adjoint ‚ÑÇ E E _ hE‚ÇÅ hE‚ÇÅ hE‚ÇÇ hE‚ÇÇ hE hE f = 0 := sorry

  -- back to the error...

  -- one way to fix this is by specifying the function:
  let f' : @LinearMap ‚ÑÇ ‚ÑÇ _ _ (RingHom.id ‚ÑÇ) E E (NormedAddCommGroup.toAddCommGroup.toAddCommMonoid)
    (NormedAddCommGroup.toAddCommGroup.toAddCommMonoid)
    (NormedSpace.toModule) (NormedSpace.toModule) := sorry
  have this' : LinearMap.adjoint f' = 0 := sorry

  -- not very practical, but it did get rid of the above error...
  sorry
}

-- unless you resolve the diamond problem, you will always get an error if you try to access
-- both the algebra and inner product space at the same time:

example (E : Type) [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] ‚ÑÇ) [Ring E] [Algebra ‚ÑÇ E] [hE : FiniteDimensional ‚ÑÇ E] :
  LinearMap.adjoint (Algebra.linearMap ‚ÑÇ E) = T :=
sorry

-- so we need to learn how to resolve the diamonds in order to move on
end example_intro

section diamonds

/-
 type `A` with instance `B : Ring A` and `C : AddCommGroup A` means we have the following type inheritance diagram:

  B   C
   \ /
    A
-/

/-
 **Diamonds** are when we have the following type inheritance diagram:
    D
   / \
  B   C
   \ /
    A
  i.e., a diamond is when we have `D` being inferred by both `B` and `C` which are inferred by `A`.

 We call a diamond **transparent** when `D` (inferred by `B`) is definitionally equal to
  `D` (inferred by `C`). We also sometimes say that a diamond is **resolved** when there are no
  non-transparent sub-diamonds (i.e., diamonds within the diamond). In other words, when the error
  is resolved.
-/

section
variable {A : Type} [B : Ring A] [C : AddCommGroup A]

-- now let `D` be `AddCommMonoid A`, and we know that this is inferred by `B` and `C`:
#check B.toAddCommMonoid
#check C.toAddCommMonoid

/-
 so then we have a diamond inheritance:
    D
   / \
  B   C
   \ /
    A
-/
end

-- :TODO:
-- determine what the diamond here is:
example (E : Type) [h‚ÇÅ : Add E] [Zero E] [One E] [h‚ÇÇ : Distrib E] [h‚ÇÉ : Mul E] :
  E :=
sorry
/-
diamond 1:
   Add
   / \
  h‚ÇÅ  h‚ÇÇ
   \ /
    E

diamond 2:
   Mul
   / \
  h‚ÇÉ  h‚ÇÇ
   \ /
    E
-/
--

end diamonds

section simple_example

/-! ## Resolving diamonds (simple example) -/

-- this was how `AddMonoidWithOne` used to be defined 7 months ago in Lean 3:
private class add_monoid_with_one (A : Type) extends AddMonoid A, One A

-- :TODO:
-- show that there exists a type such that `A` is an `add_monoid_with_one` and a `Ring` such that
-- they have different ones
example :
  ‚àÉ (A : Type) (h‚ÇÅ : add_monoid_with_one A) (h‚ÇÇ : Ring A), h‚ÇÅ.one ‚â† h‚ÇÇ.one :=
‚ü®‚ÑÇ, { one := 0, toAddMonoid := AddLeftCancelMonoid.toAddMonoid },
  Complex.instRingComplex, by norm_num‚ü©

-- `AddMonoidWithOne` in Lean 4 is stronger
-- however, Lean does not know if the ones are definitionally equal:

example (A : Type) [h‚ÇÅ : AddMonoidWithOne A] [h‚ÇÇ : Ring A] :
  h‚ÇÅ.one = h‚ÇÇ.one :=
rfl -- fails

-- :TODO:
-- show that `1 : A` in `AddMonoidWithOne` is `1 : ‚Ñï` coerced into `A`
example (A : Type) [AddMonoidWithOne A] :
  (1 : A) = (1 : ‚Ñï) :=
Nat.cast_one.symm

-- in general:
-- given any classes `classB` and `classC` such that they both extend the same class `sameBC`,
-- then if `classB` and `classC` are instances of `A`, then it does not necessarily mean that
-- the value of `x` from `classB` is the same as the one from `classC`
private class sameBC (A : Type) :=
(x : A)
(add : A ‚Üí A ‚Üí A)

private class classB (A : Type) extends sameBC A :=
(p‚ÇÇ : A ‚Üí A)
private class classC (A : Type) extends sameBC A :=
(p‚ÇÉ : A ‚Üí A ‚Üí A)

-- :TODO:
example :
  ‚àÉ (A : Type) (hB : classB A) (hC : classC A), hB.x ‚â† hC.x :=
‚ü®‚Ñ§, { x := 0, add := (¬∑+¬∑), p‚ÇÇ := Œª _ => 0},
  { x := 1, add := (¬∑-¬∑), p‚ÇÉ := Œª _ => 0},
  by simp only [ne_eq, zero_ne_one, not_false_eq_true]‚ü©

 section solution_1
 -- solution 1 - adding the necessary constraints to the statement, for example:
 example (A : Type) (hB : classB A) (hC : classC A) (hBC : hB.x = hC.x) :
   hB.x = hC.x :=
 hBC

 -- in theory, giving the properties of the classes you want as instances to your type
 -- is the safest method for avoiding diamond instance problems

 -- for example, instead of saying `[classA] [classC]` we can write
 -- `(x : A) (add : A ‚Üí A ‚Üí A) (p‚ÇÇ : A ‚Üí A) (p‚ÇÉ : A ‚Üí A ‚Üí A)`

 -- in practice, however, this is obviously **not** practical, and you most likely will end up
 -- with an error sooner or later
 end solution_1

 section solution_2
 -- solution 2
 private class classE (A : Type) extends classB A, classC A

 -- the above can equivalently be written as:
 private class classE' (A : Type) extends classB A :=
 (p‚ÇÉ : A ‚Üí A ‚Üí A)

 -- now we can show that any type `A` such that `classE A` (which infers both `classB`
 -- and `classC`) will always have the same `x` and `add` properties
 example (A : Type) [h : classE A] :
   h.toclassB.x = (h.toclassC).x :=
 rfl
 example (A : Type) [h : classE A] :
   h.toclassB.add = (h.toclassC).add :=
 rfl

 end solution_2

end simple_example


section resolving_diamonds
/-!
 ## Resolving diamonds (another example)
-/

/-
 We already concluded in the first section that there are non-transparent
  diamonds within our example.

 The first is `NormedAddCommGroup` clashing with `Ring`.
-/

/- note that we have the following transparent diamond `‚óá`,
  i.e., `AddCommMonoid` created by `Ring ‚Üí AddCommGroup` is definitionally equal to that
  created by `Ring ‚Üí NonUnitalNonAssocSemiring ‚Üí AddCommMonoid`. -/
example {E : Type*} [h : Ring E] :
  ((h.toAddCommGroup).toAddCommMonoid : AddCommMonoid E) = h.toAddCommMonoid :=
rfl

/-- `NormedAddCommGroup` structure with `Ring`, i.e., the properties of `Ring`
  and `NormedAddCommGroup`, but without the common property `AddCommGroup`. -/
@[class, reducible]
structure NormedAddCommGroupOfRing (E : Type*) extends Norm E, Ring E, MetricSpace E :=
dist := fun x y => ‚Äñx - y‚Äñ
/-- The distance function is induced by the norm. -/
dist_eq : ‚àÄ x y, dist x y = ‚Äñx - y‚Äñ := by aesop

-- equivalently, we can write the following instead
@[class, reducible] structure NACGoR (E : Type*) extends NormedAddCommGroup E, Ring E

instance NACGoR.inst_Ring {E : Type*} [h : NACGoR E] :
  Ring E :=
h.toRing
instance NACGoR.inst_NormedAddCommGroup {E : Type*} [h : NACGoR E] :
  NormedAddCommGroup E :=
h.toNormedAddCommGroup

/-
 Let's check that we now get the same `AddCommMonoid`:
-/
example {E : Type*} [h : NACGoR E] :
  h.toAddCommMonoid = NormedAddCommGroup.toAddCommGroup.toAddCommMonoid :=
rfl

/-
 We also get the same `AddCommGroup` structure:
-/
example {E : Type*} [NACGoR E] :
  (NormedAddCommGroup.toAddCommGroup : AddCommGroup E) = Ring.toAddCommGroup :=
rfl

/- we have resolved the first diamond! -/

-- this now works!!!
example {E : Type*} [h : NACGoR E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E] [Algebra ‚ÑÇ E] [FiniteDimensional ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) :
  LinearMap.adjoint T = 0 := by
{
  let f : E ‚Üí‚Çó[‚ÑÇ] E := sorry
  have : LinearMap.adjoint f = T := sorry
  sorry
}

-- but we still have some issues remaining to address:

example {E : Type*} [hE‚ÇÅ : NACGoR E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [hE‚ÇÉ : Algebra ‚ÑÇ E] [hE‚ÇÑ : FiniteDimensional ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) (x y : E) :
  0 = (LinearMap.adjoint (Algebra.linearMap ‚ÑÇ E)) x := sorry

/- The error is because of the following non-transparent diamond:
  the module (inferred by the inner product space) is not definitionally equal to that
  inferred by the algebra. -/
example {E : Type*} [NACGoR E]
  [h : InnerProductSpace ‚ÑÇ E] [Algebra ‚ÑÇ E] :
  h.toModule = Algebra.toModule :=
rfl -- fails

/-
 `Algebra ‚ÑÇ A` instance given `A` as a Semiring, `‚ÑÇ` Module, and
 has `SMulCommClass ‚ÑÇ A A` and `IsScalarTower ‚ÑÇ A A`
 (see [implementation notes](https://leanprover-community.github.io/mathlib4_docs/Mathlib/Algebra/Algebra/Basic.html#Implementation-notes))
-/
def Algebra.ofModule_inst {R A : Type*} [CommSemiring R] [Semiring A]
  [Module R A] [SMulCommClass R A A] [IsScalarTower R A A] : Algebra R A :=
Algebra.ofModule smul_mul_assoc mul_smul_comm

-- the reason the following is a `local` instance is because you might end up
-- having an infinite recursion of type instances
attribute [local instance] Algebra.ofModule_inst

/-
 Now the module given by the inner product space **is** definitionally equal to that
   inferred by the algebra (which is inferred by the module inferred by the inner producrt space).
-/
example {E : Type*} [NACGoR E] [h : InnerProductSpace ‚ÑÇ E] [SMulCommClass ‚ÑÇ E E]
  [IsScalarTower ‚ÑÇ E E] :
  h.toModule = Algebra.toModule :=
rfl

/- we have now resolved all of our diamonds! -/

-- ay this works now!!! üéâ
example {E : Type*} [hE‚ÇÅ : NACGoR E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [SMulCommClass ‚ÑÇ E E] [IsScalarTower ‚ÑÇ E E] [FiniteDimensional ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) (x y : E) :
  ‚ü™LinearMap.adjoint T (x * y), T x‚ü´_‚ÑÇ = (LinearMap.adjoint (Algebra.linearMap ‚ÑÇ E)) x := sorry

end resolving_diamonds
